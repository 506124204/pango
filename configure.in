dnl Process this file with autoconf to create configure.

AC_INIT(ChangeLog)
AM_INIT_AUTOMAKE(pango, 0.8)

AM_CONFIG_HEADER(config.h)

AC_PROG_CC
AM_PROG_LIBTOOL

changequote(,)dnl
if test "x$GCC" = "xyes"; then
  case " $CFLAGS " in
  *[\ \	]-Wall[\ \	]*) ;;
  *) CFLAGS="$CFLAGS -Wall" ;;
  esac
fi
changequote([,])dnl

gtk_works=no
AM_PATH_GTK(1.2.0,
            [CFLAGS="$GTK_CFLAGS $CFLAGS"; gtk_works=yes],
            AC_MSG_WARN("Disabling GTK+-based tests"))

AM_CONDITIONAL(BUILD_TESTS, test x$gtk_works = xyes)

AC_CHECK_LIB(fribidi, fribidi_map_range, :,
	     AC_MSG_ERROR([*** libfribidi not found.]))


# We run glib-config separately to get the gmodule cflags so we
# can add them to pango-config

AC_PATH_PROG(GLIB_CONFIG, glib-config, no)
if test x$GLIB_CONFIG = xno ; then
  AC_MSG_ERROR([*** glib-config not found])
fi
GLIB_CFLAGS="`glib-config --cflags gmodule`"
CFLAGS="$CFLAGS $GLIB_CFLAGS"
GLIB_LIBS="`glib-config --libs gmodule`"
AC_SUBST(GLIB_LIBS)
AC_SUBST(GLIB_CFLAGS)


AC_PATH_PROG(UNICODE_CONFIG, unicode-config, no)
if test x$UNICODE_CONFIG = xno ; then
  AC_MSG_ERROR([*** unicode-config not found])
fi
UNICODE_CFLAGS="`unicode-config --cflags`"
CFLAGS="$CFLAGS $UNICODE_CFLAGS"
UNICODE_LIBS="`unicode-config --libs`"
AC_SUBST(UNICODE_LIBS)
AC_SUBST(UNICODE_CFLAGS)

AC_CHECK_FUNCS(flockfile)

AC_CHECK_LIB(unicode, unicode_type, :,
	     AC_MSG_ERROR([*** libunicode not found.]), $UNICODE_LIBS)

AC_CHECK_PROG(gtkdoc, gtkdoc-scan, yes, no)
AM_CONDITIONAL(HAVE_GTK_DOC, test x$gtkdoc = xyes)

AC_OUTPUT([
Makefile 
pango/Makefile
modules/Makefile
modules/arabic/Makefile
modules/basic/Makefile
modules/hangul/Makefile
modules/tamil/Makefile
examples/Makefile
docs/Makefile
tools/Makefile
fonts/Makefile
pango.spec
pango-config
],[case "$CONFIG_FILES" in
*pango-config*)chmod +x pango-config;;
esac])

